// slavecomm.nxc
// 08.02.2012 T Schaer
// Slave communication code

// ----- Variables
int CommState = -1;
const string Master = MASTER;

unsigned int BadRequests, BadCommands, Timeouts, Cycles;

// Protocol timeouts
TimerObject ReqMsgFinishTimer;
TimerObject CmdMsgBeginTimer;
TimerObject CmdMsgFinishTimer;

// ----- Code
void CommInit() {
  // Set up High Speed Port
  SetSensorType(IN_4, SENSOR_TYPE_HIGHSPEED);
  SetHSState(HS_INITIALISE);
  SetHSFlags(HS_UPDATE);  
  
  // start with empty input buffer
  SetHSInputBufferInPtr(0);
  SetHSInputBufferOutPtr(0);
  
  // make sure everything gets turned on okay
  Wait(5);
  
  // Set up the timers
  ReqMsgFinishTimer.Timeout = 1000;
  CmdMsgBeginTimer.Timeout = 1000;
  CmdMsgFinishTimer.Timeout = 1000;

}


void Comm() {
  bool Done = false;
  unsigned char MsgLen, CommandMsgLen;
  string RequestMessage, ResponseMessage;  
  string CommandMessage, StatusMessage;
  string sTemp;
  TickTock CommTaskElapsed;
    
  while (Done == false) {
    switch(CommState) {
      case -3:
        // State -3: Exit state
        Cycles++;
        Done = true;
        Tock(CommTaskElapsed);
        TextOut(0, LCD_LINE7, "Elapsed : " + NumToStr(Elapsed(CommTaskElapsed)));
        CommState = -1;
        break;
      case -1:
        // State -1: Initialize
        Tick(CommTaskElapsed);
        CommTask_resetAll(PDI_C);
        CommTask_resetAll(PDO_C);
        // Get PDO
        Acquire(mxPDO);
        PDO_C = PDO;
        Release(mxPDO);
        StatusMessage = CommTask_serializeJson(Master, PDO_C);
        ResponseMessage = RespMessage(Master, StrLen(StatusMessage), 0);        
        CommState = 0;
        break;
      case 0:
        // State 0: wait for Request Message
        if (HSInputBufferInPtr() > 0) {
          StartTimer(ReqMsgFinishTimer);
          CommState = 1;
        } else {
          Wait(5);
        }
        break;
      case 1:
        // State 1: receive Request Message
        MsgLen = HSInputBufferInPtr();
        if (MsgLen >= REQMSGLEN+1) {
          GetHSInputBuffer(0, REQMSGLEN+1, RequestMessage);
          SetHSInputBufferInPtr(0);
          if (CommTask_parseJson(RequestMessage, "Request", PDI_C) == true) {
            sTemp = CommTask_lookup("len", "Request", PDI_C);
            CommandMsgLen = StrToNum(sTemp);
            SendRS485String(ResponseMessage);
            CommState = 2;
          } else {
            TextOut(0, LCD_LINE8, "Bad Request");
            BadRequests++;
            CommState = -3;            
          }
        } else if (CheckTimer(ReqMsgFinishTimer) == true) {
          Timeouts++;
          TextOut(0, LCD_LINE8, "Timeout State 1");
          CommState = -3;
        }
        break;
      case 2:
        // State 2: send Response Message
        if (SendingDone()) {
          StartTimer(CmdMsgBeginTimer);
          CommState = 3;          
        }
        break;
      case 3:
        // State 3: Wait for Command Message
       if (HSInputBufferInPtr() > 0) {
        StartTimer(CmdMsgFinishTimer);
        CommState = 4;
       } else if (CheckTimer(CmdMsgBeginTimer) == true) {
        Timeouts++;
        TextOut(0, LCD_LINE8, "Timeout State 3");
        CommState = -3;
       } else {
        Wait(5);
       }
       break;
      case 4:
        // State 4: receive Command Message
        MsgLen = HSInputBufferInPtr();
        if (MsgLen >= CommandMsgLen + 1) {
          GetHSInputBuffer(0, MsgLen, CommandMessage);
          SetHSInputBufferInPtr(0);
          // SNIP!
          SendRS485String(StatusMessage);
          CommState = 5;
        } else if (CheckTimer(CmdMsgFinishTimer) == true) {
          Timeouts++;
          TextOut(0, LCD_LINE8, "Timeout State 4");
          CommState = -3;
        }
        break;
      case 5:
        // State 5: send Status Message
        if(SendingDone()) {
          // Move here temporarily so that Status Message is sent sooner
          // start SNIP
          CommTask_resetAll(PDI_C);
          if (CommTask_parseJson(CommandMessage, Master, PDI_C) == false) {
            BadCommands++;
            TextOut(0, LCD_LINE8, "Bad Command Msg");
          }
          // end SNIP
          Acquire(mxPDI);
          CommTask_resetAll(PDI);
          PDI = PDI_C;
          Release(mxPDI);
          CommState = -3;          
        }
        break;
      default:
        // you should never get here, but...
        TextOut(0, LCD_LINE8, "w00t! Error!       ");
        Wait(10000);
        CommState = -3;
    }
  }
}

